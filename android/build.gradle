buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Fix for plugins that don't have namespace defined (required for AGP 8.0+)
subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android') && project.android.namespace == null) {
            // Try to get package from AndroidManifest.xml
            def manifest = project.file("src/main/AndroidManifest.xml")
            if (manifest.exists()) {
                def xml = new XmlSlurper().parse(manifest)
                def packageName = xml.@package.toString()
                if (packageName) {
                    project.android.namespace = packageName
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
